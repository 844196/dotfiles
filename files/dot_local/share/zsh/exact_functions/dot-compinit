# idea: https://gist.github.com/ctechols/ca1035271ad134841284

autoload -Uz compinit

setopt local_options
setopt extendedglob

local dump_path
zstyle -s ':dot:compinit:*' dump-path dump_path
: ${dump_path:=${ZDOTDIR:-$HOME}/.zcompdump}

local dump_ttl
zstyle -s ':dot:compinit:*' dump-ttl dump_ttl
: ${dump_ttl:=86400}

local -a options
zstyle -a ':dot:compinit:*' options options

if [[ -n ${dump_path}(#qN.ms+${dump_ttl}) ]]; then
	compinit ${options[@]} -d $dump_path
else
	compinit -C -d $dump_path
fi

local compile_dump
zstyle -b ':dot:compinit:*' compile-dump compile_dump

if [[ $compile_dump = 'yes' ]] && [[ ! -e ${dump_path}.zwc || ${dump_path}.zwc -ot $dump_path ]]; then
  zcompile -R $dump_path
fi
