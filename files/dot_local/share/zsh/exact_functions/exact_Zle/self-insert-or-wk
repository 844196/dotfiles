# vim: set ft=zsh:

case $BUFFER in
  '')
    tput cub 9999

    local res=''
    res=$(~/.local/libexec/wk 2>&1)
    local wk_exit=$?

    # tput cub 9999 で wk 実行時点でのカーソル位置を無理やり本来あるべき位置から変えているため
    # 終了コードに関係なくプロンプトを再描画する
    zle redisplay

    case $wk_exit in
      0)
        # NOOP
        ;;
      1)
        return 1
        ;;
      *)
        zle -M "wk: $res"
        return $wk_exit
        ;;
    esac

    res=("${(@ps:\t:)res}")

    if [[ "${res[(rb:2:)eval:*]}" == 'eval:true' ]]; then
      BUFFER=${(e)res[1]}
    else
      BUFFER=${res[1]}
    fi
    CURSOR=${#BUFFER}

    case "${res[(rb:2:)trigger:*]}" in
      trigger:ACCEPT)
        zle accept-line-with-expand-alias
        ;;
      trigger:COMPLETE)
        zle fzf-completion-or-expand-or-complete
        ;;
    esac

    zle redisplay
    ;;

  *)
    zle _expand_alias
    zle self-insert
    ;;
esac
