# vim: set ft=zsh:

emulate -L zsh
setopt EXTENDED_GLOB KSH_GLOB

case $KEYS in
  a)
    case $BUFFER in
      'git add -')
        BUFFER="git add -A"
        CURSOR=${#BUFFER}
        ;;

      *)
        zle .self-insert
        ;;
    esac
    ;;

  d)
    case $BUFFER in
      git\ *@(\ |...)(#i)(hea))
        BUFFER="${BUFFER[0,-4]}HEAD"
        CURSOR=${#BUFFER}
        ;;

      'git branch -')
        BUFFER="git branch -D"
        CURSOR=${#BUFFER}
        ;;

      *)
        zle .self-insert
        ;;
    esac
    ;;

  '^')
    case $BUFFER in
      *@(HEAD|@)*(\\^))
        BUFFER="${BUFFER}\^"
        CURSOR=${#BUFFER}
        ;;

      *)
        zle .self-insert
        ;;
    esac
    ;;

  ' ')
    case $BUFFER in
      '')
        tput cub 9999
        exec < /dev/tty
        local res=("${(@ps:\t:)$(~/.local/libexec/which-key)}")

        BUFFER="${res[1]}"
        CURSOR=${#BUFFER}

        if [[ "${res[2]}" == "ACCEPT" ]]; then
          zle .reset-prompt
          zle accept-line-with-expand-alias
        fi

        if [[ "${res[2]}" == "COMPLETE" ]]; then
          zle fzf-completion-or-expand-or-complete
        fi

        zle .reset-prompt
        ;;

      ck)
        BUFFER="git checkout "
        CURSOR=${#BUFFER}
        ;;

      'git checkout ')
        zle fzf-completion-or-expand-or-complete
        ;;

      'git checkout -b')
        zle .self-insert
        zle fzf-completion-or-expand-or-complete
        ;;

      'git branch -m')
        BUFFER="git branch -m $(git current-branch)"
        CURSOR=${#BUFFER}
        ;;

      git\ branch\ -[dD])
        zle .self-insert
        zle fzf-completion-or-expand-or-complete
        ;;

      a)
        BUFFER="git add "
        CURSOR=${#BUFFER}
        ;;

      'git add ')
        zle fzf-completion-or-expand-or-complete
        ;;

      'git commit -m')
        BUFFER="git commit -m "
        CURSOR=${#BUFFER}
        zle fzf-completion-or-expand-or-complete
        ;;

      'git commit -f')
        BUFFER="git commit --fixup "
        CURSOR=${#BUFFER}
        zle fzf-completion-or-expand-or-complete
        ;;

      'git commit -a')
        BUFFER="git commit --amend --no-edit "
        CURSOR=${#BUFFER}
        ;;

      r)
        BUFFER="git rebase "
        CURSOR=${#BUFFER}
        ;;

      'git rebase ')
        zle fzf-completion-or-expand-or-complete
        ;;

      git\ rebase\ (-i|--interactive))
        zle .self-insert
        zle fzf-completion-or-expand-or-complete
        ;;

      'git push ')
        BUFFER="git push origin HEAD "
        CURSOR=${#BUFFER}
        ;;

      'git push -u')
        BUFFER="git push -u origin HEAD "
        CURSOR=${#BUFFER}
        ;;

      git\ push\ (-f|--force))
        BUFFER="git push --force-with-lease --force-if-includes origin HEAD "
        CURSOR=${#BUFFER}
        ;;

      d)
        BUFFER="docker "
        CURSOR=${#BUFFER}
        ;;

      dr)
        BUFFER="docker run --rm -it "
        CURSOR=${#BUFFER}
        ;;

      dc)
        BUFFER="docker compose "
        CURSOR=${#BUFFER}
        ;;

      n)
        BUFFER="npm "
        CURSOR=${#BUFFER}
        ;;

      nr)
        BUFFER="npm run "
        CURSOR=${#BUFFER}
        ;;

      pc|'gh pr create')
        local gh_params=(-w)

        local base="$(git base-branch 2>/dev/null)"
        if [[ -n "${base}" ]]; then
          gh_params+=(-B ${base})
        fi

        gh_params+=(-H $(git current-branch))
        gh_params+=(-a @me)

        BUFFER="gh pr create ${gh_params} "
        CURSOR=${#BUFFER}
        ;;

      gh\ pr\ create\ *\ (-t|--title))
        zle .self-insert
        zle fzf-completion-or-expand-or-complete
        ;;

      *)
        zle _expand_alias
        zle .self-insert
        ;;
    esac
    ;;

  *)
    zle .self-insert
    ;;
esac

zle autosuggest-fetch
