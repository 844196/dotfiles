# vim: set ft=zsh:

emulate -L zsh
setopt EXTENDED_GLOB KSH_GLOB

case $KEYS in
  a)
    case $BUFFER in
      'git add -')
        BUFFER="git add -A"
        CURSOR=${#BUFFER}
        ;;

      *)
        zle .self-insert
        ;;
    esac
    ;;

  d)
    case $BUFFER in
      git\ *@(\ |...)(#i)(hea))
        BUFFER="${BUFFER[0,-4]}HEAD"
        CURSOR=${#BUFFER}
        ;;

      *)
        zle .self-insert
        ;;
    esac
    ;;

  '^')
    case $BUFFER in
      *@(HEAD|@)*(\\^))
        BUFFER="${BUFFER}\^"
        CURSOR=${#BUFFER}
        ;;

      *)
        zle .self-insert
        ;;
    esac
    ;;

  ' ')
    case $BUFFER in
      ck)
        BUFFER="git checkout "
        CURSOR=${#BUFFER}
        ;;

      'git checkout ')
        zle fzf-completion-or-expand-or-complete
        ;;

      cb|'git checkout -b')
        local base="$(git current-branch)"
        local reply="git checkout -b $(git config --local user.name)/${base}/"
        BUFFER="${reply} && git base-branch ${base}"
        CURSOR=${#reply}
        ;;

      bm|'git branch -m')
        BUFFER="git branch -m $(git rev-parse --abbrev-ref HEAD)"
        CURSOR=${#BUFFER}
        ;;

      bd|'git branch -d')
        BUFFER="git branch -D "
        CURSOR=${#BUFFER}
        zle fzf-completion-or-expand-or-complete
        ;;

      a)
        BUFFER="git add "
        CURSOR=${#BUFFER}
        ;;

      'git add ')
        zle fzf-completion-or-expand-or-complete
        ;;

      cm)
        BUFFER="git commit -m "
        CURSOR=${#BUFFER}
        zle fzf-completion-or-expand-or-complete
        ;;

      'git commit -m')
        zle .self-insert
        zle fzf-completion-or-expand-or-complete
        ;;

      cf|'git commit --fixup')
        BUFFER="git commit --fixup "
        CURSOR=${#BUFFER}

        local after=''
        local base="$(git base-commit 2>/dev/null)"
        if [[ -n "$base" ]]; then
          after=" && git -c core.editor=true rebase -i --autosquash ${base}"
        fi

        zle fzf-completion-or-expand-or-complete "$after"
        ;;

      r)
        BUFFER="git rebase "
        CURSOR=${#BUFFER}
        ;;

      'git rebase ')
        local base="$(git base-branch)"
        if [[ -n "$base" ]]; then
          BUFFER="git rebase origin/${base}"
          CURSOR=${#BUFFER}
        else
          zle fzf-completion-or-expand-or-complete
        fi
        ;;

      'ri')
        BUFFER="git rebase -i "
        CURSOR=${#BUFFER}
        zle fzf-completion-or-expand-or-complete
        ;;

      'git rebase -i')
        zle .self-insert
        zle fzf-completion-or-expand-or-complete
        ;;

      'git push -f')
        BUFFER="git push --force-with-lease --force-if-includes "
        CURSOR=${#BUFFER}
        ;;

      d)
        BUFFER="docker "
        CURSOR=${#BUFFER}
        ;;

      dr)
        BUFFER="docker run --rm -it "
        CURSOR=${#BUFFER}
        ;;

      dc)
        BUFFER="docker compose "
        CURSOR=${#BUFFER}
        ;;

      n)
        BUFFER="npm "
        CURSOR=${#BUFFER}
        ;;

      nr)
        BUFFER="npm run "
        CURSOR=${#BUFFER}
        ;;

      pa)
        BUFFER="php artisan "
        CURSOR=${#BUFFER}
        ;;

      *)
        zle _expand_alias
        zle .self-insert
        ;;
    esac
    ;;

  *)
    zle .self-insert
    ;;
esac

zle autosuggest-fetch
