# vim: set ft=zsh:

emulate -L zsh
setopt EXTENDED_GLOB KSH_GLOB

case $KEYS in
  a)
    case $BUFFER in
      'git add -')
        BUFFER="git add -A"
        CURSOR=${#BUFFER}
        ;;

      *)
        zle .self-insert
        ;;
    esac
    ;;

  d)
    case $BUFFER in
      git\ *@(\ |...)(#i)(hea))
        BUFFER="${BUFFER[0,-4]}HEAD"
        CURSOR=${#BUFFER}
        ;;

      *)
        zle .self-insert
        ;;
    esac
    ;;

  '^')
    case $BUFFER in
      *@(HEAD|@)*(\\^))
        BUFFER="${BUFFER}\^"
        CURSOR=${#BUFFER}
        ;;

      *)
        zle .self-insert
        ;;
    esac
    ;;

  ' ')
    case $BUFFER in
      ck)
        BUFFER="git checkout "
        CURSOR=${#BUFFER}
        ;;

      'git checkout ')
        zle fzf-completion-or-expand-or-complete
        ;;

      cb|'git checkout -b')
        local reply="git checkout -b $(git config --local user.name)/$(git rev-parse --abbrev-ref HEAD)/"
        BUFFER="${reply} && git commit --allow-empty -m ':new: Create topic branch'"
        CURSOR=${#reply}
        ;;

      bm|'git branch -m')
        BUFFER="git branch -m $(git rev-parse --abbrev-ref HEAD)"
        CURSOR=${#BUFFER}
        ;;

      bd|'git branch -d')
        BUFFER="git branch -D "
        CURSOR=${#BUFFER}
        zle fzf-completion-or-expand-or-complete
        ;;

      a)
        BUFFER="git add "
        CURSOR=${#BUFFER}
        ;;

      'git add ')
        zle fzf-completion-or-expand-or-complete
        ;;

      cm)
        BUFFER="git commit -m "
        CURSOR=${#BUFFER}
        zle fzf-completion-or-expand-or-complete
        ;;

      'git commit -m')
        zle .self-insert
        zle fzf-completion-or-expand-or-complete
        ;;

      cf)
        BUFFER="git commit --fixup "
        CURSOR=${#BUFFER}
        zle fzf-completion-or-expand-or-complete
        ;;

      'git commit --fixup')
        zle .self-insert
        zle fzf-completion-or-expand-or-complete
        ;;

      r)
        BUFFER="git rebase "
        CURSOR=${#BUFFER}
        ;;

      'git rebase ')
        zle fzf-completion-or-expand-or-complete
        ;;

      'ri')
        BUFFER="git rebase -i "
        CURSOR=${#BUFFER}
        zle fzf-completion-or-expand-or-complete
        ;;

      'git rebase -i')
        zle .self-insert
        zle fzf-completion-or-expand-or-complete
        ;;

      'git push -f')
        BUFFER="git push --force-with-lease --force-if-includes "
        CURSOR=${#BUFFER}
        ;;

      d)
        BUFFER="docker "
        CURSOR=${#BUFFER}
        ;;

      dr)
        BUFFER="docker run --rm -it "
        CURSOR=${#BUFFER}
        ;;

      dc)
        BUFFER="docker compose "
        CURSOR=${#BUFFER}
        ;;

      n)
        BUFFER="npm "
        CURSOR=${#BUFFER}
        ;;

      nr)
        BUFFER="npm run "
        CURSOR=${#BUFFER}
        ;;

      *)
        zle _expand_alias
        zle .self-insert
        ;;
    esac
    ;;

  *)
    zle .self-insert
    ;;
esac

zle autosuggest-fetch
