# vim: set ft=zsh:
# cSpell:words psvar rprompt zformat

declare -gA __ZEN_SYMBOLS_DEFAULT=(
  round:open $'\ue0b6'
  round:close $'\ue0b4'
  prompt:ok $'\u276f'
  prompt:ng $'\u276f'
  git:branch:dirty '*'
  git:status:unmerged $'\u2aef'
  git:status:ahead $'\u21e1'
  git:status:behind $'\u21e3'
)
declare -gA __ZEN_COLORS_DEFAULT=(
  prompt:ok:fg '#414868'
  prompt:ng:fg '#f7768e'
  user:bg '#7aa2f7'
  user:fg '#16161e'
  path:bg '#24283b'
  path:fg '#787c99'
  git:branch:bg '#1d2230'
  git:branch:fg '#bb9af7'
  git:branch:dirty:fg '#a0a9cb'
  git:status:unmerged:fg '#414868'
  git:status:ahead:fg '#414868'
  git:status:behind:fg '#414868'
  git:user:fg '#414868'
)
declare -gA __ZEN_PSVAR_INDEXES=(
  user 1
  pwd 2
  git:branch 3
  git:branch:dirty 4
  git:status:ahead 5
  git:status:behind 6
  git:status:unmerged 7
  git:user 8
)

prompt_zen_refresh_ps() {
  setopt LOCAL_OPTIONS EXTENDED_GLOB

  local -A __ZEN_SYMBOLS=("${(@kv)__ZEN_SYMBOLS_DEFAULT}")
  local -A __ZEN_COLORS=("${(@kv)__ZEN_COLORS_DEFAULT}")

  local key default pattern style defined
  for key default in ${(kv)__ZEN_SYMBOLS}; do
    pattern=":prompt:zen:${key}"

    zstyle -t $pattern symbol "$default"
    case $? in
      1)
        zstyle -s $pattern symbol defined
        __ZEN_SYMBOLS[$key]="$defined"
        ;;
      2)
        __ZEN_SYMBOLS[$key]="$default"
        ;;
    esac
  done
  for key default in ${(kv)__ZEN_COLORS}; do
    pattern=":prompt:zen:${key%:[^:]##}"
    style="${key##?##:}"

    zstyle -t $pattern $style "$default"
    case $? in
      1)
        zstyle -s $pattern $style defined
        __ZEN_COLORS[$key]="$defined"
        ;;
      2)
        __ZEN_COLORS[$key]="$default"
        ;;
    esac
  done

  local ps=(
    $'\n'
    "%(${__ZEN_PSVAR_INDEXES[user]}V."
      "%F{$__ZEN_COLORS[user:bg]}$__ZEN_SYMBOLS[round:open]%K{$__ZEN_COLORS[user:bg]}"
      " %F{$__ZEN_COLORS[user:fg]}%${__ZEN_PSVAR_INDEXES[user]}v%f "
      "%k%F{$__ZEN_COLORS[user:bg]}%K{$__ZEN_COLORS[path:bg]}$__ZEN_SYMBOLS[round:close]%f"
    "."
      "%F{$__ZEN_COLORS[path:bg]}$__ZEN_SYMBOLS[round:open]%K{$__ZEN_COLORS[path:bg]}"
    ")"

    " %F{$__ZEN_COLORS[path:fg]}%${__ZEN_PSVAR_INDEXES[pwd]}v%f "

    "%k%F{$__ZEN_COLORS[path:bg]}%K{$__ZEN_COLORS[git:branch:bg]}$__ZEN_SYMBOLS[round:close]%f"
    "%(${__ZEN_PSVAR_INDEXES[git:branch]}V."
      "  %F{$__ZEN_COLORS[git:branch:fg]}%${__ZEN_PSVAR_INDEXES[git:branch]}v%f"
      "%(${__ZEN_PSVAR_INDEXES[git:branch:dirty]}V."
        " %F{$__ZEN_COLORS[git:branch:dirty:fg]}$__ZEN_SYMBOLS[git:branch:dirty]%f"
      ".)"
      " "
    ". )"
    "%k%F{$__ZEN_COLORS[git:branch:bg]}$__ZEN_SYMBOLS[round:close]%f"

    " %F{$__ZEN_COLORS[git:status:fg]}"
    "%(${__ZEN_PSVAR_INDEXES[git:status:unmerged]}V.%F{$__ZEN_COLORS[git:status:unmerged:fg]}$__ZEN_SYMBOLS[git:status:unmerged]%f .)"
    "%(${__ZEN_PSVAR_INDEXES[git:status:ahead]}V.%F{$__ZEN_COLORS[git:status:ahead:fg]}$__ZEN_SYMBOLS[git:status:ahead]%f .)"
    "%(${__ZEN_PSVAR_INDEXES[git:status:behind]}V.%F{$__ZEN_COLORS[git:status:behind:fg]}$__ZEN_SYMBOLS[git:status:behind]%f.)"
    "%f"

    $'\n'
    "%(?."
      "%F{$__ZEN_COLORS[prompt:ok:fg]}$__ZEN_SYMBOLS[prompt:ok]%f "
    "."
      "%F{$__ZEN_COLORS[prompt:ng:fg]}$__ZEN_SYMBOLS[prompt:ng]%f "
    ")"
  )
  PROMPT=${(j..)ps}

  if zstyle -T ':prompt:zen:right-prompt' enable; then
    RPROMPT="%(${__ZEN_PSVAR_INDEXES[git:user]}V.%F{$__ZEN_COLORS[git:user:fg]}%${__ZEN_PSVAR_INDEXES[git:user]}v%f.)"
  fi
}

prompt_zen_show_instant_prompt() {
  prompt_zen_refresh_ps

  # PROMPT_CR と PROMPT_SP は prompt_zen_clear_instant_prompt で有効化されるので、ここではセットしない
  # セットするとインスタントプロンプト消去時に PROMPT_EOL_MARK が表示されて表示が崩れてしまう
  unsetopt PROMPT_CR PROMPT_SP

  psvar=()
  psvar[$__ZEN_PSVAR_INDEXES[user]]="$(prompt_zen_user)"
  psvar[$__ZEN_PSVAR_INDEXES[pwd]]="$(prompt_zen_pwd)"

  # \e7 -> Save cursor position
  print -rn -- $'\e7'${(%)PROMPT}

  prompt_zen_clear_instant_prompt() {
    setopt PROMPT_CR PROMPT_SP

    # \e[?25l -> Hide cursor
    # \e8     -> Restore cursor position
    # \e[0m   -> Reset all attributes
    # \e[J    -> Clear from cursor to end of screen
    # \e[?25h -> Show cursor
    print -rn $'\e[?25l\e8\e[0m\e[J\e[?25h'

    add-zsh-hook -d precmd prompt_zen_clear_instant_prompt
  }

  add-zsh-hook precmd prompt_zen_clear_instant_prompt
}

prompt_zen_setup() {
  prompt_zen_refresh_ps

  prompt_opts=(percent)

  if zstyle -T ':prompt:zen' async; then
    add-zsh-hook precmd prompt_zen_precmd_async
    zle -N prompt_zen_precmd_async_callback
  else
    add-zsh-hook precmd prompt_zen_precmd_sync
  fi

  if (( ${precmd_functions[(I)prompt_zen_clear_instant_prompt]} > 0 )); then
    # 必ず最後に実行されるようにしないと、画面がちらつく
    precmd_functions=(${(@)precmd_functions:#prompt_zen_clear_instant_prompt} prompt_zen_clear_instant_prompt)
  else
    prompt_opts+=(cr sp)
  fi
}

prompt_zen_precmd_sync() {
  {
    psvar=()
    psvar[$__ZEN_PSVAR_INDEXES[user]]="$(prompt_zen_user)"
    psvar[$__ZEN_PSVAR_INDEXES[pwd]]="$(prompt_zen_pwd)"

    prompt_zen_set_psvar "$(prompt_zen_git-branch)"
    prompt_zen_set_psvar "$(prompt_zen_git-user)"
  } always {
    # Ensure next hook will run
    # See https://zsh.sourceforge.io/Doc/Release/Functions.html#index-hook-functions
    :
  }
}

prompt_zen_precmd_async() {
  local fd_git_branch fd_git_user
  {
    psvar=()
    psvar[$__ZEN_PSVAR_INDEXES[user]]="$(prompt_zen_user)"
    psvar[$__ZEN_PSVAR_INDEXES[pwd]]="$(prompt_zen_pwd)"

    exec {fd_git_branch}< <(prompt_zen_git-branch)
    zle -Fw $fd_git_branch prompt_zen_precmd_async_callback

    exec {fd_git_user}< <(prompt_zen_git-user)
    zle -Fw $fd_git_user prompt_zen_precmd_async_callback
  } always {
    # Ensure next hook will run
    # See https://zsh.sourceforge.io/Doc/Release/Functions.html#index-hook-functions
    :
  }
}

prompt_zen_user() {
  local format
  zstyle -T ':prompt:zen:user' format
  case $? in
    0)
      format='%n'
      ;;
    1)
      zstyle -s ':prompt:zen:user' format format
      ;;
  esac

  print -rn -- ${(%)format}
}

prompt_zen_pwd() {
  setopt LOCAL_OPTIONS EXTENDED_GLOB

  # https://zsh.sourceforge.io/Doc/Release/Expansion.html#index-glob-operators
  local git_match=((../)#.git(#qN))

  if (( $+git_match[1] )); then
    local git_root=${git_match[1]:A:h}
    print -rn -- ${git_root:t}${PWD#$git_root}
  else
    print -rn -- ${(%):-%~}
  fi
}

prompt_zen_git-branch() {
  if ! git rev-parse --is-inside-work-tree &>/dev/null; then
    return
  fi

  setopt LOCAL_OPTIONS EXTENDED_GLOB

  local fd_git_unmerged_changes
  {
    local statuses=("${(f)"$(
      # Avoid index update (i.e. .git/index.lock remain)
      # See https://git-scm.com/docs/git-status#_background_refresh
      git --no-optional-locks status --branch --porcelain=2 -unormal 2>/dev/null # cspell:disable-line
    )"}")
    local current_branch=${statuses[(r)\# branch.head *]#\# branch.head }

    # Depends $current_branch
    exec {fd_git_unmerged_changes}< <(
      # Git aliases was very slow
      # local base_branch=$(git base-branch 2>/dev/null)
      local base_branch=$(git config "branch.${current_branch}.base" 2>/dev/null)

      if [[ -n "$base_branch" ]]; then
        local count=$(git rev-list --no-merges --count origin/${base_branch}..HEAD)
        if (( count > 0 )); then
          echo $count
        fi
      fi
    )

    local dirty=${${statuses:#\# *}:+1}
    local branch_ab=${statuses[(r)\# branch.ab *]}
    local ahead=${branch_ab[(ws. .r)+[1-9][0-9]#]#+}
    local behind=${branch_ab[(ws. .r)-[1-9][0-9]#]#-}
    local unmerged_changes=$(<&${fd_git_unmerged_changes})

    local chunks=(
      "${__ZEN_PSVAR_INDEXES[git:branch]}:${current_branch}"
      "${__ZEN_PSVAR_INDEXES[git:branch:dirty]}:${dirty}"
      "${__ZEN_PSVAR_INDEXES[git:status:ahead]}:${ahead}"
      "${__ZEN_PSVAR_INDEXES[git:status:behind]}:${behind}"
      "${__ZEN_PSVAR_INDEXES[git:status:unmerged]}:${unmerged_changes}"
    )

    print -n -- ${(pj.\t.)chunks}
  } always {
    exec {fd_git_unmerged_changes}>&-
  }
}

prompt_zen_git-user() {
  if ! git rev-parse --is-inside-work-tree &>/dev/null; then
    return
  fi

  local format
  zstyle -T ':prompt:zen:git:user' format
  case $? in
    0)
      format='%u'
      ;;
    1)
      zstyle -s ':prompt:zen:git:user' format format
      ;;
  esac

  local formatted
  zformat -F formatted "$format" "u:$(git config --local user.name 2>/dev/null)"

  print -n -- "${__ZEN_PSVAR_INDEXES[git:user]}:${formatted}"
}

prompt_zen_set_psvar() {
  local segment
  for segment in "${(ps:\t:)@}"; do
    local chunks=(${(s.:.)segment})

    local idx=${chunks[1]}
    local value=${chunks[2]}

    psvar[$idx]=$value
  done
}

prompt_zen_precmd_async_callback() {
  local fd=$1
  {
    zle -F $fd

    local REPLY
    read -ru $fd
    [[ -z $REPLY ]] &&
      return

    prompt_zen_set_psvar "$REPLY"

    zle && [[ $CONTEXT == start ]] &&
      zle reset-prompt
  } always {
    exec {fd}>&-
  }
}

case "$1" in
  instant)
    prompt_zen_show_instant_prompt
    ;;
  *)
    prompt_zen_setup
    ;;
esac
