# vim: set ft=zsh:

autoload -Uz add-zsh-hook
autoload -Uz select-word-style
autoload -Uz history-search-end
autoload -Uz smart-insert-last-word
autoload -Uz historyignore
autoload -Uz send-osc7
autoload -Uz completioninit
autoload -Uz evalcache
autoload -Uz toggle-delta-features
autoload -Uz compute-anyframe-fzf-options
autoload -Uz accept-line-with-expand-alias
autoload -Uz self-insert-with-expand-alias
autoload -Uz fzf-completion-or-expand-or-complete
autoload -Uz promptinit
autoload -Uz up-line-or-beginning-search
autoload -Uz down-line-or-beginning-search
autoload -Uz edit-command-line-shim
autoload -Uz zman
autoload -Uz anyframe-init
autoload -Uz zsh-defer

add-zsh-hook precmd send-osc7
add-zsh-hook precmd toggle-delta-features
add-zsh-hook preexec toggle-delta-features
add-zsh-hook zshaddhistory historyignore

disable r # It's so dangerous!
unsetopt FLOW_CONTROL # Disable Ctrl+S and Ctrl+Q
unsetopt BEEP
setopt PRINT_EIGHT_BIT
setopt EXTENDED_GLOB
setopt INTERACTIVE_COMMENTS

zstyle ':completion' dump-file $XDG_CACHE_HOME/zsh/compdump-$ZSH_VERSION
zstyle ':completion' dump-ttl $((60 * 60 * 18))
zstyle ':completion:*' menu select=2
zstyle ':completion:*' verbose yes
zstyle ':completion:*' list-packed yes
zstyle ':completion:*' matcher-list 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' # Ignore case
zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path $XDG_CACHE_HOME/zsh/compcache
zstyle ':completion:*' completer _expand _complete _match _approximate _files
zstyle -e ':completion:*:approximate:*' max-errors 'reply=($((($#PREFIX + $#SUFFIX) / 3)) numeric)'
setopt MAGIC_EQUAL_SUBST
setopt GLOB_DOTS

HISTFILE=$XDG_STATE_HOME/zsh/history
HISTSIZE=1000000
SAVEHIST=1000000
HISTORY_IGNORE='(:|true|false|pwd|which *|type *|where *|exit|(|z)man(| *)|neofetch(| *)|*--help|*--version)'
setopt EXTENDED_HISTORY
setopt SHARE_HISTORY
setopt HIST_REDUCE_BLANKS
setopt HIST_IGNORE_SPACE
setopt HIST_FIND_NO_DUPS

select-word-style default
anyframe-init
zle -N accept-line-with-expand-alias
zle -N self-insert-with-expand-alias
zle -N fzf-completion-or-expand-or-complete
zle -N history-beginning-search-forward-end history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
zle -N insert-last-word smart-insert-last-word
zle -N edit-command-line edit-command-line-shim
zstyle ':zle:*' word-style unspecified
zstyle ':zle:*' word-chars ' /=;@:{}[]()<>,|.'
zstyle ':insert-last-word' match '*([[:alpha:]/\\]?|?[[:alpha:]/\\])*'
zstyle -e ':anyframe:selector:fzf:*' command 'reply=(fzf $(compute-anyframe-fzf-options))'

alias ..='cd ..'
alias ...='cd ../..'
alias ls=' eza --time-style=long-iso -s=type -logF -a'
alias tree=' eza -T -I=.git --git-ignore -s=type -a'
alias cp='cp -i'
alias mv='mv -i'
alias rm='rm -I --preserve-root -r' # '--preserve-root' is only available for GNU rm
alias tmp='cd "$(mktemp -d)"'
alias b='git branch'
alias c='git commit'
alias st=' git status'
alias di=' git diff'
alias gg=' git graph -n 10'
alias ch='chezmoi'

bindkey '^N' history-beginning-search-forward-end
bindkey '^P' history-beginning-search-backward-end
bindkey '^[OA' up-line-or-beginning-search # Up arrow
bindkey '^[OB' down-line-or-beginning-search # Down arrow
bindkey '^[[Z' reverse-menu-complete # Shift+Tab
bindkey '^M' accept-line-with-expand-alias
bindkey 'a' self-insert-with-expand-alias
bindkey 'd' self-insert-with-expand-alias
bindkey '\^' self-insert-with-expand-alias
bindkey ' ' self-insert-with-expand-alias
bindkey '^I' fzf-completion-or-expand-or-complete
bindkey '^R' anyframe-widget-put-history
bindkey '^J' anyframe-widget-jump
bindkey '^X^B' anyframe-widget-insert-git-branch-name
bindkey '^X^F' anyframe-widget-insert-git-status-file
bindkey '^X^X' anyframe-widget-insert-git-commit-hash
bindkey '^X^G' anyframe-widget-insert-gitmoji
bindkey '^X^E' edit-command-line

evalcache navi widget zsh
evalcache zoxide init zsh --no-cmd
evalcache direnv hook zsh

completioninit

# Must be loaded after loading all other plugins (except zsh-autosuggestions, see below)
zsh-defer -a +pr source $XDG_DATA_HOME/zsh/plugins/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh

# Autosuggestions must be initialized after syntax highlighting
# see: https://github.com/romkatv/zsh-bench#deferred-initialization
# see: https://github.com/zsh-users/zsh-autosuggestions#disabling-automatic-widget-re-binding
ZSH_AUTOSUGGEST_MANUAL_REBIND=1
zsh-defer -a +ms source $XDG_DATA_HOME/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh

ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=20
ZSH_AUTOSUGGEST_STRATEGY=(history completion)
ZSH_AUTOSUGGEST_CLEAR_WIDGETS+=(
  accept-line-with-expand-alias
  history-beginning-search-backward-end
  history-beginning-search-forward-end
  up-line-or-beginning-search
  down-line-or-beginning-search
)

# ?(...) - https://zsh.sourceforge.io/Doc/Release/Expansion.html#ksh_002dlike-Glob-Operators
# #cN, - https://zsh.sourceforge.io/Doc/Release/Expansion.html#Globbing-Flags
ZSH_AUTOSUGGEST_HISTORY_IGNORE='( eza *|cd *|\\(cp|mv|rm) *|git (add|branch|checkout) *|?(#c50,))'
ZSH_AUTOSUGGEST_COMPLETION_IGNORE='( eza *|cd *|\\(cp|mv|rm) *|git (add|branch|checkout) *)'

promptinit
prompt zen
setopt TRANSIENT_RPROMPT

if [[ -f $ZDOTDIR/.zshrc.local ]]; then
  source $ZDOTDIR/.zshrc.local
fi
