# vim: set ft=zsh:

autoload -Uz promptinit && promptinit

if [[ "${VSCODE_COPILOT_TERMINAL-0}" = 1 ]]; then
  source "$(code --locate-shell-integration-path zsh)"

  # 高確率でコマンド出力の検出に失敗するため無効化
  zstyle ':prompt:zen' async no
  zstyle ':prompt:zen:right-prompt' enable no

  # 人間用と区別しやすくする
  zstyle ':prompt:zen:user' format $'\uf4b8 '
else
  zstyle ':prompt:zen:user' format ''
  prompt zen instant
fi

autoload -Uz $XDG_DATA_HOME/zsh/functions/**/*(N.:t)
autoload -Uz add-zsh-hook
autoload -Uz select-word-style
autoload -Uz smart-insert-last-word
autoload -Uz {up,down}-line-or-beginning-search
autoload -Uz zman
autoload -Uz anyframe-init

add-zsh-hook precmd send-osc7
add-zsh-hook precmd toggle-delta-features
add-zsh-hook preexec toggle-delta-features
add-zsh-hook zshaddhistory historyignore

disable r # It's so dangerous!
unsetopt FLOW_CONTROL # Disable Ctrl+S and Ctrl+Q
unsetopt BEEP
unsetopt PATH_DIRS # Avoid slow completion on WSL2
setopt EXTENDED_GLOB
setopt INTERACTIVE_COMMENTS

evalcache ~/.local/libexec/vivid-compat tokyonight-night
export BAT_THEME="tokyonight_night"
export FZF_DEFAULT_OPTS=$FZF_DEFAULT_OPTS"
  --color=bg+:#283457 \
  --color=bg:-1 \
  --color=border:#27a1b9 \
  --color=fg:-1 \
  --color=gutter:-1 \
  --color=header:-1 \
  --color=hl+:#2ac3de \
  --color=hl:#2ac3de \
  --color=info:#545c7e \
  --color=marker:#ff007c \
  --color=pointer:#ff007c \
  --color=prompt:#2ac3de \
  --color=query:#c0caf5:regular \
  --color=scrollbar:#27a1b9 \
  --color=separator:#ff9e64 \
  --color=spinner:#ff007c \
"

# https://zsh.sourceforge.io/Doc/Release/Parameters.html#index-ZLE_005fREMOVE_005fSUFFIX_005fCHARS
ZLE_REMOVE_SUFFIX_CHARS=
ZLE_SPACE_SUFFIX_CHARS=$'&|'
zstyle ':completion' dump-file $XDG_CACHE_HOME/zsh/compdump-$ZSH_VERSION
zstyle ':completion' dump-ttl $((60 * 60 * 18))
zstyle ':completion:*' list-separator '│'
zstyle ':completion:*' list-colors ${(s.:.)${LS_COLORS/no=([^:])#/}} 'no=0;38;5;8' 'ma=0;38;5;0;48;5;8'
zstyle ':completion:*' menu select=2
zstyle ':completion:*' verbose yes
zstyle ':completion:*' list-packed yes
zstyle ':completion:*' matcher-list 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' # Ignore case
zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path $XDG_CACHE_HOME/zsh/compcache
zstyle ':completion:*' completer _expand _complete _match _approximate _files
zstyle ':completion:*:*:chezmoi:*:*' ignored-patterns '^(init|apply)'
zstyle -e ':completion:*:approximate:*' max-errors 'reply=($((($#PREFIX + $#SUFFIX) / 3)) numeric)'

# 日本語を含む補完候補リストを正しく表示するための設定だが LC_CTYPE にUTF-8ロケールが設定されている場合は不要
# setopt PRINT_EIGHT_BIT

# "git reset HEAD^" や "mise run check:*" のようなグロブ展開しようと思って入力したわけではないグロブ記号使用時に "no matches found" エラーが発生しないようにする
unsetopt NOMATCH

# anything=expression のような形式の引数の時、expression先頭の ~ および = を展開する
# e.g. anything=~/foobar -> anything=/home/me/foobar
#      anything==grep -> anything=/usr/bin/grep
# また、補完関数によって明示的に指定されていない場合、expressionでファイル名補完が利用可能になる (文書化されていない)
# https://unix.stackexchange.com/a/599449
setopt MAGIC_EQUAL_SUBST

# グロブ展開する際、入力の先頭にドットがなくてもドットから始まるファイルもマッチ対象にする
setopt GLOB_DOTS

# Workaround: When VSCode shell integration feature is enabled, HISTFILE is never empty
# https://github.com/microsoft/vscode/issues/169264
HISTFILE=${ZHISTFILE:-$XDG_STATE_HOME/zsh/history}
HISTSIZE=1000000
SAVEHIST=1000000
HISTORY_IGNORE_PATTERNS=(
  ':'
  'true'
  'false'
  'pwd'
  'cd'
  'which *'
  'type *'
  'where *'
  'exit'
  '(z|)man(| *)'
  'neofetch(| *)'
  'chezmoi ##apply'
  '*tempCodeRunnerFile*'
  '*--help'
  '*--version'
)
HISTORY_IGNORE="(${(j.|.)HISTORY_IGNORE_PATTERNS})"
setopt SHARE_HISTORY
setopt EXTENDED_HISTORY
setopt HIST_REDUCE_BLANKS
setopt HIST_IGNORE_SPACE
setopt HIST_FIND_NO_DUPS
setopt HIST_FCNTL_LOCK

# https://news.ycombinator.com/item?id=26757438
# https://www.reddit.com/r/zsh/comments/c160o2/comment/erbdiqj/?utm_source=share&utm_medium=web3x&utm_name=web3xcss&utm_term=1&utm_content=share_button
zle_highlight+=(paste:none)
select-word-style default
anyframe-init
evalcache wk init --major-prefix 'm'
zstyle ':zle:*' word-style unspecified
zstyle ':zle:*' word-chars ' /=;@:{}[]()<>,|.'
zstyle ':insert-last-word' match '*([[:alpha:]/\\]?|?[[:alpha:]/\\])*'
zstyle -e ':anyframe:selector:fzf:*' command 'reply=($GLOBAL_AQUA_ROOT_DIR/bin-copied/fzf $(compute-anyframe-fzf-options))'
zstyle ':wk:*' options '--up-one-line false'
zstyle ':wk:*' accept-widget accept-line-with-expand-alias

bindkey '^[[Z' reverse-menu-complete # Shift+Tab
bindkey '^R' anyframe-widget-put-history
bindkey '^X^B' anyframe-widget-insert-git-branch-name
bindkey '^X^F' anyframe-widget-insert-git-status-file
bindkey '^X^X' anyframe-widget-insert-git-commit-hash
bindkey '^X^G' anyframe-widget-insert-gitmoji
bindwidget '^[.' insert-last-word smart-insert-last-word
bindwidget '^[OA' up-line-or-beginning-search # Up arrow
bindwidget '^[OB' down-line-or-beginning-search # Down arrow
bindwidget '^M' accept-line-with-expand-alias
bindwidget '\^' self-insert-with-tweak
bindwidget ' ' self-insert-or-trigger-wk
bindwidget ',' self-insert-or-trigger-wk-major
bindwidget '^I' fzf-completion-or-expand-or-complete
bindwidget '^[q' push-line-or-edit-with-show
bindwidget '^Q' push-line-or-edit-with-show # `NO_FLOW_CONTROL` required
bindwidget '^G' toggle-incognito
bindwidget '^X^E' edit-command-line edit-command-line-shim

alias ..='cd ..'
alias ...='cd ../..'
alias ls=' eza'
alias ll=' eza -l'
alias la=' eza -la'
alias cp='cp -i'
alias mv='mv -i'
alias rm='rm -I --preserve-root -r' # '--preserve-root' is only available for GNU rm
alias t='cd "$(mktemp -d)"'
alias mx='mise x --'
alias mr='mise run'

evalcache zoxide init zsh --no-cmd

# zshenv で追加したshimエントリーはこの時点で削除され
# chpwd と precmd に "各プロジェクトで設定されたツール実体の場所を PATH の最上位に追加するフック" が登録される
# https://mise.jdx.dev/dev-tools/shims.html#how-to-add-mise-shims-to-path
evalcache mise activate zsh

completioninit

ZSH_AUTOSUGGEST_MANUAL_REBIND=1
source $XDG_DATA_HOME/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh

ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=black,bold"
ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=20
ZSH_AUTOSUGGEST_STRATEGY=(history completion)
ZSH_AUTOSUGGEST_CLEAR_WIDGETS+=(accept-line-with-expand-alias up-line-or-beginning-search down-line-or-beginning-search)
ZSH_AUTOSUGGEST_HISTORY_IGNORE='( eza *|cd *|\\(cp|mv|rm) *|git (add|branch|checkout) *|gh pr create *|?(#c50,))'

if [[ -f $ZDOTDIR/.zshrc.local ]]; then
  source $ZDOTDIR/.zshrc.local
fi

setopt TRANSIENT_RPROMPT
prompt zen
