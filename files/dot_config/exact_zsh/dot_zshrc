# vim: set ft=zsh:

autoload -Uz add-zsh-hook
autoload -Uz select-word-style
autoload -Uz smart-insert-last-word
autoload -Uz {up,down}-line-or-beginning-search
autoload -Uz promptinit
autoload -Uz zman
autoload -Uz anyframe-init
autoload -Uz $XDG_DATA_HOME/zsh/functions/**/*(N.:t)

# Avoid flickering on clear instant prompt due to direnv output via precmd hook
# https://github.com/romkatv/powerlevel10k/blob/119e4039ef9068fa96490a90c559e7594843ec22/README.md#how-do-i-initialize-direnv-when-using-instant-prompt
eval "$(direnv export zsh)"

prompt_zen_setup instant-pre

add-zsh-hook precmd send-osc7
add-zsh-hook precmd toggle-delta-features
add-zsh-hook preexec toggle-delta-features
add-zsh-hook zshaddhistory historyignore

disable r # It's so dangerous!
unsetopt FLOW_CONTROL # Disable Ctrl+S and Ctrl+Q
unsetopt BEEP
setopt PRINT_EIGHT_BIT
setopt EXTENDED_GLOB
setopt INTERACTIVE_COMMENTS

zstyle ':completion' dump-file $XDG_CACHE_HOME/zsh/compdump-$ZSH_VERSION
zstyle ':completion' dump-ttl $((60 * 60 * 18))
zstyle ':completion:*' menu select=2
zstyle ':completion:*' verbose yes
zstyle ':completion:*' list-packed yes
zstyle ':completion:*' matcher-list 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' # Ignore case
zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path $XDG_CACHE_HOME/zsh/compcache
zstyle ':completion:*' completer _expand _complete _match _approximate _files
zstyle ':completion:*:*:chezmoi:*:*' ignored-patterns '^(init|apply)'
zstyle -e ':completion:*:approximate:*' max-errors 'reply=($((($#PREFIX + $#SUFFIX) / 3)) numeric)'
setopt MAGIC_EQUAL_SUBST
setopt GLOB_DOTS

# Workaround: When VSCode shell integration feature is enabled, HISTFILE is never empty
# https://github.com/microsoft/vscode/issues/169264
HISTFILE=${ZHISTFILE:-$XDG_STATE_HOME/zsh/history}
HISTSIZE=1000000
SAVEHIST=1000000
HISTORY_IGNORE_PATTERNS=(
  ':'
  'true'
  'false'
  'pwd'
  'cd'
  'which *'
  'type *'
  'where *'
  'exit'
  '(z|)man(| *)'
  'neofetch(| *)'
  'chezmoi ##apply'
  '*tempCodeRunnerFile*'
  '*--help'
  '*--version'
)
HISTORY_IGNORE="(${(j.|.)HISTORY_IGNORE_PATTERNS})"
setopt SHARE_HISTORY
setopt EXTENDED_HISTORY
setopt HIST_REDUCE_BLANKS
setopt HIST_IGNORE_SPACE
setopt HIST_FIND_NO_DUPS
# For bind HISTFILE to Docker container
# https://www.reddit.com/r/zsh/comments/1evpegt/history_without_renaming_zsh_historynew_to_zsh/
unsetopt HIST_SAVE_BY_COPY
setopt HIST_FCNTL_LOCK

select-word-style default
anyframe-init
zle -N accept-line-with-expand-alias
zle -N self-insert-with-tweak
zle -N self-insert-or-trigger-wk
zle -N fzf-completion-or-expand-or-complete
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
zle -N insert-last-word smart-insert-last-word
zle -N edit-command-line edit-command-line-shim
zle -N push-line-or-edit-with-show
zle -N toggle-incognito
zstyle ':zle:*' word-style unspecified
zstyle ':zle:*' word-chars ' /=;@:{}[]()<>,|.'
zstyle ':insert-last-word' match '*([[:alpha:]/\\]?|?[[:alpha:]/\\])*'
zstyle -e ':anyframe:selector:fzf:*' command 'reply=($GLOBAL_AQUA_ROOT_DIR/bin-copied/fzf $(compute-anyframe-fzf-options))'

bindkey '^[OA' up-line-or-beginning-search # Up arrow
bindkey '^[OB' down-line-or-beginning-search # Down arrow
bindkey '^[[Z' reverse-menu-complete # Shift+Tab
bindkey '^M' accept-line-with-expand-alias
bindkey '\^' self-insert-with-tweak
bindkey ' ' self-insert-or-trigger-wk
bindkey '^I' fzf-completion-or-expand-or-complete
bindkey '^R' anyframe-widget-put-history
bindkey '^[q' push-line-or-edit-with-show
bindkey '^Q' push-line-or-edit-with-show # `NO_FLOW_CONTROL` required
bindkey '^G' toggle-incognito
bindkey '^X^B' anyframe-widget-insert-git-branch-name
bindkey '^X^F' anyframe-widget-insert-git-status-file
bindkey '^X^X' anyframe-widget-insert-git-commit-hash
bindkey '^X^G' anyframe-widget-insert-gitmoji
bindkey '^X^E' edit-command-line

alias ..='cd ..'
alias ...='cd ../..'
alias ls=' eza --time-style=long-iso -s=type -logF -a'
alias tree=' eza -T -I=.git --git-ignore -s=type -a'
alias cp='cp -i'
alias mv='mv -i'
alias rm='rm -I --preserve-root -r' # '--preserve-root' is only available for GNU rm
alias tmp='cd "$(mktemp -d)"'

evalcache zoxide init zsh --no-cmd
evalcache direnv hook zsh

evalcache wk widget zsh --no-bindkey
zstyle ':wk:*' options '--no-validation'
zstyle ':wk:*' accept-widget accept-line-with-expand-alias

completioninit

# Must be loaded after loading all other plugins (except zsh-autosuggestions, see below)
source $XDG_DATA_HOME/zsh/plugins/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh

# Autosuggestions must be initialized after syntax highlighting
# see: https://github.com/romkatv/zsh-bench#deferred-initialization
# see: https://github.com/zsh-users/zsh-autosuggestions#disabling-automatic-widget-re-binding
ZSH_AUTOSUGGEST_MANUAL_REBIND=1
source $XDG_DATA_HOME/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh

ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=20
ZSH_AUTOSUGGEST_STRATEGY=(history completion)
ZSH_AUTOSUGGEST_CLEAR_WIDGETS+=(accept-line-with-expand-alias up-line-or-beginning-search down-line-or-beginning-search)
ZSH_AUTOSUGGEST_HISTORY_IGNORE='( eza *|cd *|\\(cp|mv|rm) *|git (add|branch|checkout) *|gh pr create *|?(#c50,))'

promptinit
prompt zen
setopt TRANSIENT_RPROMPT

if [[ -f $ZDOTDIR/.zshrc.local ]]; then
  source $ZDOTDIR/.zshrc.local
fi

prompt_zen_setup instant-post
