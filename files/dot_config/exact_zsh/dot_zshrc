# vim: set ft=zsh:

autoload -Uz add-zsh-hook
autoload -Uz select-word-style
autoload -Uz smart-insert-last-word
autoload -Uz {up,down}-line-or-beginning-search
autoload -Uz promptinit
autoload -Uz zman
autoload -Uz anyframe-init
autoload -Uz $XDG_DATA_HOME/zsh/functions/**/*(N.:t)

# Avoid flickering on clear instant prompt due to direnv output via precmd hook
# https://github.com/romkatv/powerlevel10k/blob/119e4039ef9068fa96490a90c559e7594843ec22/README.md#how-do-i-initialize-direnv-when-using-instant-prompt
eval "$(direnv export zsh)"

prompt_zen_setup instant-pre

add-zsh-hook precmd send-osc7
add-zsh-hook precmd toggle-delta-features
add-zsh-hook preexec toggle-delta-features
add-zsh-hook zshaddhistory historyignore

disable r # It's so dangerous!
unsetopt FLOW_CONTROL # Disable Ctrl+S and Ctrl+Q
unsetopt BEEP
unsetopt PATH_DIRS # Avoid slow completion on WSL2
setopt PRINT_EIGHT_BIT
setopt EXTENDED_GLOB
setopt INTERACTIVE_COMMENTS

evalcache ~/.local/libexec/vivid-compat iceberg-dark
export BAT_THEME="iceberg"
export FZF_DEFAULT_OPTS=$FZF_DEFAULT_OPTS"
  --color gutter:-1
  --color prompt:8
  --color info:8
  --color header:8
  --color border:0
  --color pointer:8
  --color marker:3
  --color fg:-1
  --color bg:-1
  --color fg+:-1
  --color bg+:0
  --color hl:3
  --color hl+:3
"

# https://zsh.sourceforge.io/Doc/Release/Parameters.html#index-ZLE_005fREMOVE_005fSUFFIX_005fCHARS
ZLE_REMOVE_SUFFIX_CHARS=
ZLE_SPACE_SUFFIX_CHARS=$'&|'
zstyle ':completion' dump-file $XDG_CACHE_HOME/zsh/compdump-$ZSH_VERSION
zstyle ':completion' dump-ttl $((60 * 60 * 18))
zstyle ':completion:*' list-separator 'â”‚'
zstyle ':completion:*' list-colors ${(s.:.)${LS_COLORS/no=([^:])#/}} 'no=0;38;5;8' 'ma=0;38;5;0;48;5;8'
zstyle ':completion:*' menu select=2
zstyle ':completion:*' verbose yes
zstyle ':completion:*' list-packed yes
zstyle ':completion:*' matcher-list 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' # Ignore case
zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path $XDG_CACHE_HOME/zsh/compcache
zstyle ':completion:*' completer _expand _complete _match _approximate _files
zstyle ':completion:*:*:chezmoi:*:*' ignored-patterns '^(init|apply)'
zstyle -e ':completion:*:approximate:*' max-errors 'reply=($((($#PREFIX + $#SUFFIX) / 3)) numeric)'
setopt MAGIC_EQUAL_SUBST
setopt GLOB_DOTS

# Workaround: When VSCode shell integration feature is enabled, HISTFILE is never empty
# https://github.com/microsoft/vscode/issues/169264
HISTFILE=${ZHISTFILE:-$XDG_STATE_HOME/zsh/history}
HISTSIZE=1000000
SAVEHIST=1000000
HISTORY_IGNORE_PATTERNS=(
  ':'
  'true'
  'false'
  'pwd'
  'cd'
  'which *'
  'type *'
  'where *'
  'exit'
  '(z|)man(| *)'
  'neofetch(| *)'
  'chezmoi ##apply'
  '*tempCodeRunnerFile*'
  '*--help'
  '*--version'
)
HISTORY_IGNORE="(${(j.|.)HISTORY_IGNORE_PATTERNS})"
setopt SHARE_HISTORY
setopt EXTENDED_HISTORY
setopt HIST_REDUCE_BLANKS
setopt HIST_IGNORE_SPACE
setopt HIST_FIND_NO_DUPS
setopt HIST_FCNTL_LOCK

# https://news.ycombinator.com/item?id=26757438
# https://www.reddit.com/r/zsh/comments/c160o2/comment/erbdiqj/?utm_source=share&utm_medium=web3x&utm_name=web3xcss&utm_term=1&utm_content=share_button
zle_highlight+=(paste:none)
select-word-style default
anyframe-init
evalcache wk --init ''
zstyle ':zle:*' word-style unspecified
zstyle ':zle:*' word-chars ' /=;@:{}[]()<>,|.'
zstyle ':insert-last-word' match '*([[:alpha:]/\\]?|?[[:alpha:]/\\])*'
zstyle -e ':anyframe:selector:fzf:*' command 'reply=($GLOBAL_AQUA_ROOT_DIR/bin-copied/fzf $(compute-anyframe-fzf-options))'
zstyle ':wk:*' options '--up-one-line false'
zstyle ':wk:*' accept-widget accept-line-with-expand-alias

bindkey '^[[Z' reverse-menu-complete # Shift+Tab
bindkey '^R' anyframe-widget-put-history
bindkey '^X^B' anyframe-widget-insert-git-branch-name
bindkey '^X^F' anyframe-widget-insert-git-status-file
bindkey '^X^X' anyframe-widget-insert-git-commit-hash
bindkey '^X^G' anyframe-widget-insert-gitmoji
bindwidget '^[.' insert-last-word smart-insert-last-word
bindwidget '^[OA' up-line-or-beginning-search # Up arrow
bindwidget '^[OB' down-line-or-beginning-search # Down arrow
bindwidget '^M' accept-line-with-expand-alias
bindwidget '\^' self-insert-with-tweak
self-insert-or-trigger-wk() {
  case $BUFFER in
    '')
      echo -n $'\x1b[?25l\x1b[0`' >$TTY # Avoid cursor flickering

      local reply
      zle _wk_widget

      case "${reply[(rb:2:)trigger:*]}" in
        trigger:COMPLETE)
          zle fzf-completion-or-expand-or-complete
          ;;
      esac
      ;;

    *)
      if zle _expand_alias; then
        # Consider ZLE_REMOVE_SUFFIX_CHARS and ZLE_SPACE_SUFFIX_CHARS
        :
      else
        zle self-insert
      fi
      ;;
  esac
}
bindwidget ' ' self-insert-or-trigger-wk
bindwidget '^I' fzf-completion-or-expand-or-complete
bindwidget '^[q' push-line-or-edit-with-show
bindwidget '^Q' push-line-or-edit-with-show # `NO_FLOW_CONTROL` required
bindwidget '^G' toggle-incognito
bindwidget '^X^E' edit-command-line edit-command-line-shim

alias ..='cd ..'
alias ...='cd ../..'
alias ls=' eza --time-style=long-iso -s=type -logF -a'
alias tree=' eza -T -I=.git --git-ignore -s=type -a'
alias cp='cp -i'
alias mv='mv -i'
alias rm='rm -I --preserve-root -r' # '--preserve-root' is only available for GNU rm
alias tmp='cd "$(mktemp -d)"'

evalcache zoxide init zsh --no-cmd
evalcache direnv hook zsh

completioninit

ZSH_AUTOSUGGEST_MANUAL_REBIND=1
source $XDG_DATA_HOME/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh

ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=black,bold"
ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=20
ZSH_AUTOSUGGEST_STRATEGY=(history completion)
ZSH_AUTOSUGGEST_CLEAR_WIDGETS+=(accept-line-with-expand-alias up-line-or-beginning-search down-line-or-beginning-search)
ZSH_AUTOSUGGEST_HISTORY_IGNORE='( eza *|cd *|\\(cp|mv|rm) *|git (add|branch|checkout) *|gh pr create *|?(#c50,))'

promptinit
prompt zen
setopt TRANSIENT_RPROMPT

if [[ -f $ZDOTDIR/.zshrc.local ]]; then
  source $ZDOTDIR/.zshrc.local
fi

prompt_zen_setup instant-post
