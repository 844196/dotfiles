source $XDG_CONFIG_HOME/dircolors
source $XDG_CONFIG_HOME/fzf
source $XDG_CONFIG_HOME/aliases

fpath=($XDG_DATA_HOME/zsh/functions(N-/) $fpath)

autoload -Uz select-word-style; select-word-style default
autoload -Uz colors; colors

zstyle ':zle:*' word-chars " /=;@:{},|"
zstyle ':zle:*' word-style unspecified

zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z} r:|[-_.]=**' '+m:{A-Z}={a-z} r:|[-_.]=**'
zstyle ':completion:*:default' menu select=2
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
bindkey '^[[Z' reverse-menu-complete
setopt list_packed
setopt nolistbeep
setopt list_rows_first
setopt magic_equal_subst

setopt auto_pushd
setopt pushd_ignore_dups

setopt no_flow_control
setopt print_eight_bit
setopt transient_rprompt
if [ "$TERM_PROGRAM" = "vscode" ]; then
  # see: https://superuser.com/questions/1391414/why-am-i-having-a-sign-between-the-lines-in-integrated-terminal-in-vs-code
  unsetopt PROMPT_SP
fi

HISTFILE=$XDG_STATE_HOME/zsh/history
HISTSIZE=1000
SAVEHIST=100000
setopt share_history
setopt hist_no_store
setopt hist_reduce_blanks
setopt hist_ignore_all_dups

zshaddhistory() {
  local line=${1%%$'\n'}
  local cmd=${line%% *}

  [[ ${#line} -ge 5
    && $cmd != (cd|gp|pwd)
    && $cmd != (ls|exa)
    && $cmd != (less|bat)
    && $line != *--(help|version)
    && $line != (git status|git graph)
  ]]
}

autoload -Uz dot-widget-init && dot-widget-init

bindkey '^M' dot-widget-accept-line-with-expand-alias
bindkey ' ' dot-widget-self-insert-with-expand-alias
bindkey '^[[1;2A' dot-widget-pushd-up-with-reset-prompt
bindkey '^[[1;2B' dot-widget-popd-with-reset-prompt

PROMPT="
%{$fg[blue]%}%n@%m:%~%{$reset_color%}
%{%(?.$fg_bold[black].$fg[red])%}%#%{$reset_color%} "

if [ -e $XDG_DATA_HOME/zsh/plugins/anyframe ]; then
  fpath=($XDG_DATA_HOME/zsh/plugins/anyframe(N-/) $fpath)
  autoload -Uz anyframe-init
  anyframe-init

  bindkey '^r' anyframe-widget-put-history-alt
  bindkey '^x^b' anyframe-widget-insert-git-branch-alt
  bindkey '^x^f' anyframe-widget-insert-git-file
fi

if [ -e $XDG_DATA_HOME/zsh/plugins/zsh-autosuggestions ]; then
  ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=black,bold"
  ZSH_AUTOSUGGEST_CLEAR_WIDGETS=(dot-widget-accept-line-with-expand-alias)

  source $XDG_DATA_HOME/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh
fi

if [ -e $XDG_DATA_HOME/zsh/plugins/zsh-syntax-highlighting ]; then
  typeset -A ZSH_HIGHLIGHT_STYLES

  # see: https://github.com/sheepla/dotfiles/blob/fd01d60b3b35fff10f5643ac7db9fca81ea32bb5/.zshrc#L282-L315
  ZSH_HIGHLIGHT_STYLES[command]='fg=white,bold'
  ZSH_HIGHLIGHT_STYLES[arg0]='fg=white,bold,underline'
  ZSH_HIGHLIGHT_STYLES[path]='fg=white,underline'
  ZSH_HIGHLIGHT_STYLES[alias]='fg=white,bold,underline'
  ZSH_HIGHLIGHT_STYLES[reserved-word]='fg=yellow,bold'
  ZSH_HIGHLIGHT_STYLES[unknown-token]='fg=red'
  ZSH_HIGHLIGHT_STYLES[single-hyphen-option]='fg=green'
  ZSH_HIGHLIGHT_STYLES[double-hyphen-option]='fg=green'
  ZSH_HIGHLIGHT_STYLES[single-quoted-argument]='fg=cyan,underline'
  ZSH_HIGHLIGHT_STYLES[dollar-quoted-argument]='fg=cyan,bold,underline'
  ZSH_HIGHLIGHT_STYLES[double-quoted-argument]='fg=blue,underline'
  ZSH_HIGHLIGHT_STYLES[dollar-double-quoted-argument]='fg=blue,bold,underline'
  ZSH_HIGHLIGHT_STYLES[command-substitution-delimiter]='fg=white,bold'
  ZSH_HIGHLIGHT_STYLES[process-substitution-delimiter]='fg=white,bold'
  ZSH_HIGHLIGHT_STYLES[back-quoted-argument-delimiter]='fg=white,bold'

  source $XDG_DATA_HOME/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

if [ -e $XDG_DATA_HOME/zsh/plugins/zsh-completions ]; then
  fpath=($XDG_DATA_HOME/zsh/plugins/zsh-completions/src(N-/) $fpath)
fi

typeset -U path fpath

autoload -Uz compinit && compinit -d $XDG_CACHE_HOME/zsh/compdump

if [ -n "${commands[starship]}" ]; then
  eval "$(starship init zsh)"
fi

if [ -n "${commands[direnv]}" ]; then
  eval "$(direnv hook zsh)"
fi
