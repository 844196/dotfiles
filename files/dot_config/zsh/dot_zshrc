autoload -Uz select-word-style
autoload -Uz history-search-end
autoload -Uz smart-insert-last-word
autoload -Uz dot-compinit
autoload -Uz dot-widgetinit
autoload -Uz dot-historyignore
autoload -Uz dot-send-osc7
autoload -Uz dot-evalcache
autoload -Uz dot-toggle-delta-features
autoload -Uz pbcopy pbpaste

# hooks
precmd_functions+=(dot-send-osc7)
preexec_functions+=(dot-toggle-delta-features)
zshaddhistory_functions+=(dot-historyignore)

# core
unsetopt FLOW_CONTROL
setopt PRINT_EIGHT_BIT
zstyle ':dot:evalcache:*' cache-dir ~/.cache/zsh/evalcache

# colors
dot-evalcache ~/.local/libexec/vivid-compat iceberg-dark

# aliases
alias ..='cd ..'
alias t='cd "$(mktemp -d)"'
alias st=' git status'
alias ck='git checkout'
alias br='git branch -vv'
alias co='git commit'
alias di=' git diff'
alias gg=' git graph -n 15'
alias c='chezmoi'
alias ls=' exa -gF --icons --git --color=always -la'
alias grep='rg'
alias find='fd'
alias df='duf'
alias du='dust'
alias d='docker'
alias dc='docker compose'
alias nr='npm run'
alias -g C='| pbcopy'

# word-chars
select-word-style default
zstyle ':zle:*' word-chars " /=;@:{},|"
zstyle ':zle:*' word-style unspecified

# completions
unsetopt LIST_BEEP
setopt MAGIC_EQUAL_SUBST
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS} # Must be after execution of vivid-compat (i.e. LS_COLORS loaded)
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z} r:|[-_.]=**' '+m:{A-Z}={a-z} r:|[-_.]=**'
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.cache/zsh/compcache
zstyle ':dot:compinit:*' dump-path ~/.cache/zsh/compdump
zstyle ':dot:compinit:*' dump-ttl $((60 * 60 * 24 * 7))
zstyle ':dot:compinit:*' compile-dump yes

# histories
HISTFILE=~/.local/state/zsh/history
HISTSIZE=1000000
SAVEHIST=1000000
HISTORY_IGNORE='(pwd|which *|type *|exit|man *|zman|less *|bat *|*--help|*--version)'
setopt EXTENDED_HISTORY
setopt SHARE_HISTORY
setopt HIST_REDUCE_BLANKS
setopt HIST_IGNORE_SPACE
setopt HIST_FIND_NO_DUPS

# keybindings
dot-widgetinit
zle -N history-beginning-search-forward-end history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N insert-last-word smart-insert-last-word
zstyle ':insert-last-word' match '*([[:alpha:]/\\]?|?[[:alpha:]/\\])*'
bindkey '^N' history-beginning-search-forward-end
bindkey '^P' history-beginning-search-backward-end
bindkey '^[[Z' reverse-menu-complete
bindkey '^M' dot-expand-alias-and-accept-line
bindkey ' ' dot-expand-alias-and-self-insert
bindkey '^R' dot-put-history
bindkey '^X^B' dot-insert-git-branch
bindkey '^X^F' dot-insert-git-file
bindkey '^X^G' dot-cd-ghq
bindkey '^X^J' dot-cd-zoxide

# plugins
dot-evalcache sheldon source

# setup cli tools
zsh-defer source ~/.config/fzf/config.sh
zsh-defer dot-evalcache navi widget zsh
zsh-defer dot-evalcache zoxide init zsh --no-cmd
zsh-defer dot-evalcache direnv hook zsh

# prompt
setopt TRANSIENT_RPROMPT
dot-evalcache starship init zsh --print-full-init # Must be execution at end of zshrc
